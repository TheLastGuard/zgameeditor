COMPILER Zc

FRAME "delphi.frm"

uses = (. Zc_Ops, Contnrs, uSymTab, ZClasses, Compiler, SysUtils, ZExpressions, ZApplication .)

public = (.
    ZFunctions : TObjectList;
    SymTab : TSymbolTable;
    AllowInitializer : boolean;
    GlobalNames : TObjectList;
    ZApp : TZApplication;
    InitializerFunction : TZcOpFunctionUserDefined;
    destructor Destroy; override;
    procedure CheckHomograph(var sym: Integer); override;
    procedure ParseEvalExpression;
.)
private = (.
    IsInIdent,IsInInvokeArg : boolean;
    CurrentFunction : TZcOpFunctionUserDefined;
    procedure CleanUp;
    procedure ZError(const S : string);
    procedure AddSwitchLabel(SwitchOp : TZcOpSwitch; var Op: TZcOp);
    procedure OnParserError(Sender: TObject; ErrorType,ErrorCode, line,col: Integer; const Msg, data: string);
    function IsConst : boolean;
    function GetInitializer : TZcOpFunctionUserDefined;
    .)
precreate = (.
    ZFunctions := TObjectList.Create(False);
    Self.OnError := OnParserError;

.)

semErrors = (.
    200 : Result := 'Not a floating point value';
    201 : Result := 'Name already defined in this scope';
    202 : Result := 'Return value expected';
    203 : Result := 'Function should not return a value';
    204 : Result := 'Only function definitions are allowed here';
    205 : Result := 'Syntax not supported';
.)

implementation = (.

procedure TZc.OnParserError(Sender: TObject; ErrorType,ErrorCode, line,col: Integer; const Msg, data: string);
begin
  ZError(Msg + ' ' + Data);
end;

procedure TZc.CheckHomograph(var sym: Integer);
begin
  //Only Homographs are name of component datatypes
  //If inside identifier "x.y.z" then let it refer to identifier instead
  if IsInIdent or IsInInvokeArg then
    sym := identSym;
end;

destructor TZc.Destroy;
begin
  inherited;
  CleanUp;
end;

procedure TZc.AddSwitchLabel(SwitchOp : TZcOpSwitch; var Op: TZcOp);
begin
  if Op=nil then
  begin
    if SwitchOp.HasDefault then
      ZError('Only one default statement is allowed.');
    SwitchOp.HasDefault := True;
  end else
    Op := MakeCompatible(Op,SwitchOp.ValueOp.GetDataType);
  SwitchOp.CaseOps.Last.Children.Add( Op );
  Op := nil;
end;

procedure TZc.CleanUp;
begin
  ZFunctions.Free;
end;

procedure TZc.ZError(const S : string);
var
  E : EParseError;
  P : TSymbolRec;
begin
  E := EParseError.Create(S);
  E.Message := S;
  Scanner.GetPosition(P);
  E.Line := P.Line;
  E.Col := P.Col;
  if (E.Line<>0) or (E.Col<>0) then
    E.Message := E.Message + ' (line: ' + IntToStr(E.Line) + ', col: ' + IntToStr(E.Col) + ')';
  raise E;
end;

function TZc.IsConst : boolean;
begin
  Result := (CurrentInputSymbol=constSym) or
    ((CurrentInputSymbol=privateSym) and (Symbols[2].Id=constSym));
end;

function TZc.GetInitializer : TZcOpFunctionUserDefined;
begin
  if not Self.AllowInitializer then
    ZError('Initializers only allowed in ZLibrary components located in App.OnLoaded');
  if not Assigned(InitializerFunction) then
  begin
    Self.InitializerFunction := TZcOpFunctionUserDefined.Create(nil);
    Self.ZFunctions.Insert(0,Self.InitializerFunction);
  end;
  Result := Self.InitializerFunction;
end;

procedure TZc.ParseEvalExpression;
var
  OutOp : TZcOp;
  Typ : TZcDataType;
  Func : TZcOpFunctionUserDefined;
begin
  Reinit;
  OutOp := nil;
  _Expr(OutOp);
  Typ := OutOp.GetDataType;

  Func := TZcOpFunctionUserDefined.Create(nil);
  Func.ReturnType := Typ;
  Self.CurrentFunction := Func;

  if Typ.Kind<>zctVoid then
  begin
    OutOp := MakeOp(zcReturn,[OutOp]);
  end;
  Func.Statements.Add(OutOp);
  Self.ZFunctions.Add(Func);
end;
.)

END


CHARACTERS

        tab                = CHR(9). /*  9 = tabulator */
        eol                = CHR(10). /* 10 = line feed */
        cr                 = CHR(13). /* 13 = carriage return */
        newLine            = cr + eol. /* Line separator character (U+2028) + Paragraph separator character (U+2029) */

        letter             = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz_".
        digit              = "0123456789".
        hexDigit           = digit + "ABCDEFabcdef".
        notDigit           = ANY - digit.

        char               = ANY - "'" - '\' - newLine.
        verbatimStringChar = ANY - '"'.
        regularStringChar  = ANY - '"' - '\' - newLine.
        notNewLine         = ANY - newLine .


TOKENS

  /*--------------------------------------------------------------------------------*/
        intCon =
                ( digit {digit}
                | ("0x" | "0X") hexDigit {hexDigit}
                )
                /* ["U" | "u" | "L" | "l" | "UL" | "Ul" | "uL" | "ul" | "LU" | "Lu" | "lU" | "lu"] */
                .
  /*--------------------------------------------------------------------------------*/
        realCon =
                "." digit {digit}
                [("e" | "E") ["+" | "-"] digit {digit}]
                ["F" | "f" | "D" | "d" | "M" | "m"]
        | digit {digit}
                ( "." digit {digit}
                        [("e" | "E" ) ["+" | "-"] digit {digit} ]
                        ["F" | "f" | "D" | "d" | "M" | "m"]
                | ("e" | "E") ["+" | "-"] digit {digit}
                        ["F" | "f" | "D" | "d" | "M" | "m"]
                | "F" | "f" | "D" | "d" | "M" | "m"
                ).
  /*--------------------------------------------------------------------------------*/
        stringCon =
                '"'    { regularStringChar
          | "\'" | '\"' | "\\" | "\n"
          }
                '"'.

        ident = letter { letter | digit }.


        /*----- operators and special characters needed in LL(1) resolvers --------------*/
        and    = "&".
        assgn  = "=".
        colon  = ":".
        comma  = ",".
        dec    = "--".
        div    = "/".
        dot    = ".".
        eq     = "==".
        gt     = ">".
        gte    = ">=".
        inc    = "++".
        lbrace = "{".
        lbrack = "[".
        lpar   = "(".
        lshift = "<<".
        lt     = "<".
        lte    = "<=".
        minus  = "-".
        mod    = "%".
        neq    = "!=".
        not    = "!".
        or     = "|".
        plus   = "+".
        rbrace = "}".
        rbrack = "]".
        rpar   = ")".
        rshift = ">>".
        scolon = ";".
        tilde  = "~".
        times  = "*".
        xor    = "^".



COMMENTS FROM "/*" TO "*/"
COMMENTS FROM "//" TO eol

IGNORE eol + cr + tab

HOMOGRAPHS
  "Material"
  "Sound"
  "Shader" 
  "Bitmap" 
  "Mesh"
  "Camera" 
  "Font"
  "Sample"
  "File"
  "Component"

PRODUCTIONS

/*------------------------------------------------------------------------*
 *--------------------------- Declarations -------------------------------*
 *------------------------------------------------------------------------*/

ZcFuncRest<Typ : TZcDataType; const Name : string; IsPrivate,IsInline : boolean>
(.
var
  Func : TZcOpFunctionUserDefined;
.)
=
     (.
        if SymTab.Contains(Name) then
          ZError('Name already defined: ' + Name);

        if IsPrivate then
          Func := TZcOpFunctionUserDefined.Create(nil)
        else
          Func := TZcOpFunctionUserDefined.Create(GlobalNames);
        Func.Id := Name;
        Func.ReturnType := Typ;
        Func.IsInline := IsInline;
        Self.CurrentFunction := Func;
        SymTab.PushScope;
        try
     .)
    [ FormalParams ] ")"
      (.
        Func.MangledName := MangleFunc(Name,CurrentFunction.Arguments.Count);
        if SymTab.Contains(Func.MangledName) then
          ZError('Name already defined: ' + Name);
        if IsPrivate then
          SymTab.AddPrevious(Func.MangledName,Func)
        else
          SymTab.AddPrevious(Func.MangledName,Func,2);
        ZFunctions.Add(Func);
      .)
    "{" ZcFuncBody "}"
        (.
          finally
            SymTab.PopScope;
          end;
        .)
.


Zc (. var
        Typ : TZcDataType;
        Name : string;
        IsPrivate,IsInline : boolean;
        Op : TZcOp;
    .)
=
  {
    IF(. IsConst .)
      (. IsPrivate := False; .)
      [ "private" (. IsPrivate:= True; .) ]
      ConstantDeclarationList<IsPrivate>
    |
      (. IsPrivate := False; IsInline := False; .)
      [ "private" (. IsPrivate:= True; .) ]
      [ "inline" (. IsInline:= True; .) ]
      ("void" (. Typ.Kind := zctVoid; .) | Type<Typ> ) ident (. Name := LexString; .)
       /* Need to separate between global variable and method declarations */
       (
         "(" ZcFuncRest<Typ,Name,IsPrivate,IsInline>
         | GlobalVarDecl<Typ,Name,IsPrivate> ";"
       )
    |
      (. CurrentFunction := GetInitializer; .)
      Block<Op>
      (. GetInitializer.Statements.Add(Op); CurrentFunction := nil; .)
  }

.

/*------------------------------------------------------------------------*/

ZcFuncBody (. var Op : TZcOp; .)
=  {  (. Op:=nil; .)
      Statement<Op>
      (. if Op<>nil then
           CurrentFunction.Statements.Add(Op);
         Op := nil;
      .)
   }
.

/*------------------------------------------------------------------------*/
FormalParams
= ( Par ["," FormalParams] )
.
/*------------------------------------------------------------------------*/
Par (.
var
  Typ : TZcDataType;
  Arg : TZcOpArgumentVar;
  IsPtr : boolean;
.)
=  (. IsPtr := False; .)
  ["ref" (. IsPtr := True; .) ]
  Type<Typ> ident
     (.
        Typ.IsPointer := IsPtr;
        if SymTab.ScopeContains(LexString) then
          SynError(201)
        else
        begin
          Arg := TZcOpArgumentVar.Create(nil);
          Arg.Id := LexString;
          Arg.Typ := Typ;
          CurrentFunction.AddArgument(Arg);
          SymTab.Add(Arg.Id,Arg);
        end;
     .)
.
/*------------------------------------------------------------------------*/

LocalVarDecl<var OutOp : TZcOp> (. var Typ : TZcDataType; .)
= (. OutOp := nil; .)
  Type<Typ> LocalVar<Typ,OutOp> {"," LocalVar<Typ,OutOp>}
.

/*------------------------------------------------------------------------*/

LocalVar<Typ : TZcDataType; var OutOp : TZcOp> (. var Loc : TZcOpLocalVar; InitOp : TZcOp; .)
= ident
     (. 
        if SymTab.ScopeContains(LexString) then
          ZError('Name already defined: ' + LexString);

        Loc := TZcOpLocalVar.Create(nil);
        Loc.Id := LexString;
        Loc.Typ := Typ;
     .)
     [ "=" Init<InitOp> (. Loc.InitExpression:=InitOp; .) ]
     (.
        SymTab.Add(Loc.Id,Loc);
        CurrentFunction.AddLocal(Loc);

        if (Loc.Typ.Kind in [zctArray, zctMat4,zctVec2,zctVec3,zctVec4]) then
        begin
          //Alloc new local array.
          //But only do this if there isn't a initial assignment that is compatible (and doesn't result in a memcpy).
          if (not Assigned(Loc.InitExpression)) or
            (not (Loc.InitExpression.GetDataType.Kind in [Loc.Typ.Kind, zctNull])) then
          begin
            if OutOp=nil then
              OutOp := MakeOp(zcBlock);
            OutOp.Children.Add( MakeOp(zcInitLocalArray,Loc.Id) );
          end;
        end;

        if Assigned(Loc.InitExpression) then
        begin
          //Generate tree for initial assignment
          if OutOp=nil then
            OutOp := MakeOp(zcBlock);
          OutOp.Children.Add( MakeAssign(atAssign, MakeOp(zcIdentifier,Loc.Id),Loc.InitExpression) );
        end;

     .)
.

/*------------------------------------------------------------------------*/

GlobalVarRest<Typ : TZcDataType; const Name : string; IsPrivate : boolean>
(. var V : TDefineVariableBase; Glob : TZcOpGlobalVar; InitOp : TZcOp; .)
=
     (.
        if SymTab.ScopeContains(Name) then
          ZError('Name already defined: ' + Name);

        V := nil;
        if Typ.Kind in [zctInt,zctFloat,zctByte,zctXptr,zctReference] then
        begin  //Simple primitives are stored in global area
          if IsPrivate then
            Glob := TZcOpGlobalVar.Create(nil)
          else
            Glob := TZcOpGlobalVar.Create(GlobalNames);
          Glob.Lib := CompilerContext.ThisC as TZLibrary;
          Glob.Offset := Glob.Lib.GlobalAreaSize;
          Glob.Id := Name;
          Glob.Typ := Typ;
          Inc(Glob.Lib.GlobalAreaSize,SizeOf(Pointer));
          if IsPrivate then
            SymTab.Add(Name,Glob)
          else
            SymTab.AddPrevious(Name,Glob);
        end
        else if Typ.Kind=zctArray then
        begin
          TDefineArray(Typ.TheArray)._ZApp := Self.ZApp; //must have zapp set to clone
          V := TDefineArray(Typ.TheArray).Clone as TDefineVariableBase;
          V._ReferenceClassId := Typ.ReferenceClassId;
          Self.ZApp.GlobalVars.AddComponent(V);
        end
        else
        begin
          V := TDefineVariable.Create(Self.ZApp.GlobalVars);
          V._Type := Typ.Kind;
          V._ReferenceClassId := Typ.ReferenceClassId;
        end;

        if Assigned(V) then
        begin
          V.SetString('Name', AnsiString(Name));
          V.DesignerReset; //Needed to init managed variables
          if IsPrivate then
            SymTab.Add(Name,V)
          else
            SymTab.AddPrevious(Name,V);
        end;
     .)
     [ "=" Init<InitOp> (.
       GetInitializer.Statements.Add( MakeAssign(atAssign, CheckPrimary( MakeIdentifier(Name) ),InitOp) );
       .)
     ]
.


GlobalVar<Typ : TZcDataType; IsPrivate : boolean>
=
  ident GlobalVarRest<Typ,LexString,IsPrivate>
.

GlobalVarDecl<Typ : TZcDataType; const Name : string; IsPrivate : boolean>
= GlobalVarRest<Typ,Name,IsPrivate> {"," GlobalVar<Typ,IsPrivate>}
.

/*------------------------------------------------------------------------*/

Init<var OutOp : TZcOp>
= Expr<OutOp>
.

/*------------------------------------------------------------------------*/
Argument<var OutOp : TZcOp>
= /* ["ref" | "out"] */
  (. OutOp := nil; .)
  Expr<OutOp>
  (. if OutOp=nil then ZError('Missing argument'); .)
.

/*------------------------------------------------------------------------*
 *-------------------------------- Types ---------------------------------*
 *------------------------------------------------------------------------*/

/* Attribute "type" is needed for error messages in EmbeddedStatement     *
 * and for array creation expressions                                     */


Type<var Typ : TZcDataType>
(.
  var
    A : TDefineArray;
    SizeOp : TZcOp;
    I : integer;
.)
=  (. FillChar(Typ,SizeOf(Typ),0); .)
  ( SimpleType<Typ> )
  {
    (
      "["       (.
                   A := TDefineArray.Create(nil);
                   GlobalNames.Add(A);
                   A._Type := Typ.Kind;
                   Typ.Kind := zctArray;
                   Typ.TheArray := A;
                .)

         [ Expr<SizeOp> (.
            SizeOp := SizeOp.Optimize;
            if SizeOp.Kind<>zcConstLiteral then
              ZError('Array size must be a constant expression');
            A.SizeDim1 := Trunc( (SizeOp as TZcOpLiteral).Value );
                         .)
         ]

         { ","  (. if A.Dimensions=High(TArrayDimensions) then ZError('Too many array dimensions'); Inc(A.Dimensions); .)
           [ Expr<SizeOp> (.
                SizeOp := SizeOp.Optimize;
                if SizeOp.Kind<>zcConstLiteral then
                  ZError('Array size must be a constant expression');
               I := Trunc( (SizeOp as TZcOpLiteral).Value );
               if (A.Dimensions=dadTwo) then A.SizeDim2 := I else A.SizeDim3 := I;
           .) ]
         }
       "]"
    )
  }
.

/*------------------------------------------------------------------------*/

SimpleType<var Typ : TZcDataType>
(.
  var
    A : TDefineArray;
.)
/* = IntType | "float" | "double" | "decimal" | "bool" */
= "float" (. Typ.Kind := zctFloat; .)
  | "int" (. Typ.Kind := zctInt; .)
  | "byte" (. Typ.Kind := zctByte; .)
  | "string" (. Typ.Kind := zctString; .)
  | "model" (. Typ.Kind := zctModel; .)
  | "xptr" (. Typ.Kind := zctXptr; .)

  | "mat4" (.
     A := Prototypes.Mat4Array;
     Typ.Kind := zctMat4;
     Typ.TheArray := A;
    .)
  | "vec2" (.
     A := Prototypes.Vec2Array;
     Typ.Kind := zctVec2;
     Typ.TheArray := A;
  .)
  | "vec3" (.
     A := Prototypes.Vec3Array;
     Typ.Kind := zctVec3;
     Typ.TheArray := A;
  .)
  | "vec4" (.
     A := Prototypes.Vec4Array;
     Typ.Kind := zctVec4;
     Typ.TheArray := A;
  .)

  /* Changes below also must be made in homographs-section */
  | "Material" (. Typ.Kind := zctReference; Typ.ReferenceClassId := MaterialClassId; .)
  | "Sound" (. Typ.Kind := zctReference; Typ.ReferenceClassId := SoundClassId; .)
  | "Shader" (. Typ.Kind := zctReference; Typ.ReferenceClassId := ShaderClassId; .)
  | "Bitmap" (. Typ.Kind := zctReference; Typ.ReferenceClassId := ZBitmapClassId; .)
  | "Mesh" (. Typ.Kind := zctReference; Typ.ReferenceClassId := MeshClassId; .)
  | "Camera" (. Typ.Kind := zctReference; Typ.ReferenceClassId := CameraClassId; .)
  | "Font" (. Typ.Kind := zctReference; Typ.ReferenceClassId := FontClassId; .)
  | "Sample" (. Typ.Kind := zctReference; Typ.ReferenceClassId := SampleClassId; .)
  | "File" (. Typ.Kind := zctReference; Typ.ReferenceClassId := ZFileClassId; .)

  | "Component" (. Typ.Kind := zctReference; Typ.ReferenceClassId := AnyComponentClassId; .)
.

/*------------------------------------------------------------------------*/

/* IntType
= "sbyte" | "byte" | "short" | "ushort" | "int" | "uint" | "long" | "ulong" | "char"
. */


ConstantDeclarationList<IsPrivate : boolean>
(.
var
  Typ : TZcDataType;
.)
= "const" Type<Typ> ConstantDeclaration<Typ,IsPrivate> {"," ConstantDeclaration<Typ,IsPrivate> }
  ";"
.

ConstantDeclaration<var Typ : TZcDataType; IsPrivate : boolean>
(.
var
  Cns : TDefineConstant;
  Op : TZcOp;
  Lit : TZcOpLiteral;
  CnsName : string;
.)
=
ident (. CnsName := LexString; .) "=" Expr<Op>
  (.
   if not (Typ.Kind in [zctByte,zctInt,zctFloat,zctString]) then
     ZError('Only byte, int, float and string constants are allowed');

   Op := Op.Optimize;
   if not (Op is TZcOpLiteral) then
     ZError('Constant expression expected: ' + Op.ToString);

   Lit := Op as TZcOpLiteral;

   if (Lit.Typ.Kind<>Typ.Kind)
     and not ((Typ.Kind=zctByte) and (Lit.Typ.Kind=zctInt))
     and not ((Typ.Kind=zctInt) and (Lit.Typ.Kind=zctFloat))
     then
     ZError('Constant expression is not of the expected type: ' + Op.ToString);

   Cns := TDefineConstant.Create(nil);
   Cns.SetString('Name',AnsiString(CnsName));
   GlobalNames.Add(Cns);

   case Typ.Kind of
     zctByte:
       begin
         Cns._Type := zctByte;
         Cns.ByteValue := Round(Lit.Value);
       end;
     zctInt:
       begin
         Cns._Type := zctInt;
         Cns.IntValue := Round(Lit.Value);
       end;
     zctFloat:
       begin
         Cns._Type := zctFloat;
         Cns.Value := Lit.Value;
       end;
     zctString:
       begin
         Cns._Type := zctString;
         Cns.SetString('StringValue',AnsiString(Lit.StringValue));
       end;
     else
       ZError('Wrong type of constant expression: ' + GetZcTypeName(Typ));
   end;
   if IsPrivate then
     SymTab.Add(CnsName,Cns)
   else
     SymTab.AddPrevious(CnsName,Cns)
  .)
/* {"," ident "=" Expr<Op>} */
.

/*------------------------------------------------------------------------*
 *------------------------------ Statements ------------------------------*
 *------------------------------------------------------------------------*/

Statement<var OutOp : TZcOp>
= (. OutOp := nil; .)

ConstantDeclarationList<false>

/*IF IsLocalVarDecl THEN
BEGIN
  LocalVarDecl ";"
END
|  EmbeddedStatement<OutOp> */

| LocalVarDecl<OutOp> ";"
| EmbeddedStatement<OutOp>
  /* LL(1) confict between LocalVarDecl and StatementExpr: *
   * ident {"." ident} { "[" ...                           */
.

/*------------------------------------------------------------------------*/
EmbeddedStatement<var OutOp : TZcOp>
  (. var
       SwitchValueOp,Op,IfOp,IfBody,ElseOp : TZcOp;
       ForInitOp,ForCondOp,ForIncOp,ForBodyOp : TZcOp;
       WhileCondOp,WhileBodyOp : TZcOp;
       SwitchOp : TZcOpSwitch;
       Loc : TZcOpLocalVar;
  .)
= Block<OutOp>
| ";"   (. OutOp := MakeOp(zcNop); .)
| StatementExpr<OutOp> ";"
| "if" (. ElseOp := nil; .)
  "(" Expr<IfOp> ")" EmbeddedStatement<IfBody> ["else" EmbeddedStatement<ElseOp>]
        (. OutOp := MakeOp(zcIf,[IfOp,IfBody,ElseOp]); .)
 | "switch" "(" Expr<SwitchValueOp> ")"
      (.
         OutOp := MakeOp(zcBlock);

         SwitchOp := TZcOpSwitch.Create(nil);

         if (SwitchValueOp.Ref is TZcOpVariableBase) or (SwitchValueOp.Kind=zcConstLiteral) then
           SwitchOp.ValueOp := SwitchValueOp
         else
         begin
           Loc := MakeTemp(SwitchValueOp.GetDataType.Kind);
           SymTab.Add(Loc.Id,Loc);
           CurrentFunction.AddLocal(Loc);
           OutOp.Children.Add( MakeAssign(atAssign, MakeOp(zcIdentifier,Loc.Id),SwitchValueOp) );
           SwitchOp.ValueOp := MakeIdentifier(Loc.Id);
         end;

         OutOp.Children.Add(SwitchOp);
      .)
   "{" {
     (. SwitchOp.CaseOps.Add( MakeOp(zcNop) ); SwitchOp.StatementsOps.Add( MakeOp(zcNop) ); .)
     SwitchSection<SwitchOp>
   } "}"
 | "while"
     (. try
         SymTab.PushScope;
         CurrentFunction.PushScope;
         WhileCondOp := nil; WhileBodyOp := nil; .)
   "(" Expr<WhileCondOp> ")" EmbeddedStatement<WhileBodyOp>
   (.
         OutOp := MakeOp(zcWhile,[WhileCondOp,WhileBodyOp]);
       finally
         SymTab.PopScope;
         CurrentFunction.PopScope;
       end;
   .)

 /* | "do" EmbeddedStatement<Op> "while" "(" Expr<Op> ")" ";" */
 | "do"
     (. try
         SymTab.PushScope;
         CurrentFunction.PushScope;
         WhileCondOp := nil; WhileBodyOp := nil; .)
   EmbeddedStatement<WhileBodyOp>
   "while" "(" Expr<WhileCondOp> ")" ";"
   (.
         OutOp := MakeOp(zcDoWhile,[WhileCondOp,WhileBodyOp]);
       finally
         SymTab.PopScope;
         CurrentFunction.PopScope;
       end;
   .)

 | "for"
    (. try
         SymTab.PushScope;
         CurrentFunction.PushScope;
         ForInitOp :=nil; ForCondOp := nil; ForIncOp := nil; .)
   "(" [ForInit<ForInitOp>] ";" [Expr<ForCondOp>] ";" [ForInc<ForIncOp>] ")" EmbeddedStatement<ForBodyOp>
   (.
         OutOp := MakeOp(zcForLoop,[ForInitOp,ForCondOp,ForIncOp,ForBodyOp]);
       finally
         SymTab.PopScope;
         CurrentFunction.PopScope;
       end;
   .)
/* | "foreach" "(" Type ident "in" Expr<Op> ")" EmbeddedStatement<Op> */
 | "break" ";"    (. OutOp := MakeOp(zcBreak); .)
 | "continue" ";" (. OutOp := MakeOp(zcContinue); .)
 | (. Op := nil; .) "return" [ Expr<Op> ] ";"
    (.
        if (Op=nil) then
        begin
          if CurrentFunction.ReturnType.Kind<>zctVoid then
            SynError(202)
          else
            OutOp := MakeOp(zcReturn);
        end else
        begin
          if CurrentFunction.ReturnType.Kind=zctVoid then
            SynError(203)
          else
            OutOp := MakeOp(zcReturn,[ MakeCompatible(Op,CurrentFunction.ReturnType) ]);
        end;
        Inc(CurrentFunction.ReturnCount);
    .)
.
/*------------------------------------------------------------------------*/

Block<var OutOp : TZcOp>
  (. var
       Op : TZcOp;
  .)
= (. Op := nil;
     OutOp := MakeOp(zcBlock);
  .)
  "{" (. SymTab.PushScope;
         CurrentFunction.PushScope;
         try .)
     {Statement<Op> (. if Assigned(Op) then OutOp.Children.Add(Op); .) }
  "}" (.
         finally
           SymTab.PopScope;
           CurrentFunction.PopScope;
         end;
      .)
  .

/*------------------------------------------------------------------------*/
StatementExpr<var OutOp : TZcOp>           (. var Op1,Op2 : TZcOp; Kind : TZcAssignType; .)
= (. Op1 :=nil; Op2 := nil; .)
  Unary<Op1>
  ( AssignOp<Kind> Expr<Op2>
    (.
      if Op2<>nil then
        OutOp := MakeAssign(Kind,Op1,Op2);
    .)
  |                             (. OutOp := Op1; .)
  )
.

/*------------------------------------------------------------------------*/
AssignOp<var Kind : TZcAssignType>
= "="     (. Kind := atAssign; .)
  | "+="  (. Kind := atPlusAssign; .)
  | "-="  (. Kind := atMinusAssign; .)
  | "*="  (. Kind := atMulAssign; .)
  | "/="  (. Kind := atDivAssign; .)
  | "|="  (. Kind := atOrAssign; .)
  | "<<="  (. Kind := atShiftLeftAssign; .)
  | ">>="  (. Kind := atShiftRightAssign; .)
  | "&="  (. Kind := atAndAssign; .)
/*  | "+=" | "-=" | "*=" | "/=" | "%=" | "&=" | "|=" | "^=" | "<<=" | ">>=" */
.

/*------------------------------------------------------------------------*/

SwitchSection<SwitchOp : TZcOpSwitch> (. var Op : TZcOp; .)
= (. Op := nil; .)
  SwitchLabel<Op>   (. AddSwitchLabel(SwitchOp,Op); .)
  {
    SwitchLabel<Op> (. AddSwitchLabel(SwitchOp,Op); .)
  }
  Statement<Op>    (. if Assigned(Op) then SwitchOp.StatementsOps.Last.Children.Add(Op); .)
  {
     Statement<Op> (. if Assigned(Op) then SwitchOp.StatementsOps.Last.Children.Add(Op); .)
  }
.

/*------------------------------------------------------------------------*/

SwitchLabel<var OutOp : TZcOp>
= "case" Expr<OutOp> ":"
| "default" ":"
.

/*------------------------------------------------------------------------*/

ForInit<var OutOp : TZcOp>     (. var Op : TZcOp; .)
= (. Op:=nil; OutOp := MakeOp(zcBlock); .)
(
LocalVarDecl<Op> (. if Assigned(Op) then OutOp.Children.Add(Op); .)
| (. Op:=nil; .) StatementExpr<Op> (. if Assigned(Op) then OutOp.Children.Add(Op); .)
  { (. Op:=nil; .) "," StatementExpr<Op> (. if Assigned(Op) then OutOp.Children.Add(Op); .) }
)
.  

/*------------------------------------------------------------------------*/

ForInc<var OutOp : TZcOp>      (. var Op : TZcOp; .)
= (. Op:=nil;
     OutOp := MakeOp(zcBlock);
  .) 
(
StatementExpr<Op> (. if Assigned(Op) then OutOp.Children.Add(Op); .) 
{ (. Op:=nil; .) "," StatementExpr<Op> (. if Assigned(Op) then OutOp.Children.Add(Op); .) }
)
. 


/*------------------------------------------------------------------------*
 *----------------------------- Expressions ------------------------------*
 *------------------------------------------------------------------------*/


Expr<var OutOp : TZcOp>   (. var Op1,Op2 : TZcOp; Kind : TZcAssignType; .)
= (. Op1 :=nil; Op2 := nil; .)
  Unary<Op1>
  ( OrExpr<Op1,OutOp>
      ["?" Expr<Op1> ":" Expr<Op2>
         (. if (OutOp=nil) or (Op1=nil) or (Op2=nil) then
              SynError(205)
            else
              OutOp := MakeOp(zcConditional,[OutOp,Op1, MakeCompatible(Op2,Op1.GetDataType) ]);
         .)
      ]
  | AssignOp<Kind> Expr<Op2>    (. OutOp := MakeAssign(Kind,Op1, Op2); .)
  )
.
/*------------------------------------------------------------------------*/
OrExpr<InOp : TZcOp; var OutOp : TZcOp>         (. var Op,L,R : TZcOp; .)
= AndExpr<InOp,OutOp>   (. L := OutOp; .)
    {"||" Unary<Op> AndExpr<Op,R>
      (. OutOp := MakeOp(zcOr,[L,R]);
         L := OutOp; .)
    }
.


/*------------------------------------------------------------------------*/
AndExpr<InOp : TZcOp; var OutOp : TZcOp>         (. var Op,L,R : TZcOp; .)
= BitOrExpr<InOp,OutOp>  (. L := OutOp; .)
    {"&&" Unary<Op> BitOrExpr<Op,R>
      (. OutOp := MakeOp(zcAnd,[L,R]);
         L := OutOp; .)
    }
.
/*------------------------------------------------------------------------*/
BitOrExpr<InOp : TZcOp; var OutOp : TZcOp>         (. var Op,L,R : TZcOp; .)
= BitXorExpr<InOp,OutOp> (. L := OutOp; .)
  {"|" Unary<Op> BitXorExpr<Op,R>
    (. OutOp := MakeOp(zcBinaryOr,[L,R]);
       L := OutOp; .)
  }
.

/*------------------------------------------------------------------------*/
BitXorExpr<InOp : TZcOp; var OutOp : TZcOp>         (. var Op,L,R : TZcOp; .)
= BitAndExpr<InOp,OutOp> (. L := OutOp; .)
  {"^" Unary<Op> BitAndExpr<Op,R>
    (. OutOp := MakeOp(zcBinaryXor,[L,R]);
       L := OutOp; .)
  }
.

/*------------------------------------------------------------------------*/
BitAndExpr<InOp : TZcOp; var OutOp : TZcOp>         (. var Op,L,R : TZcOp; .)
= EqlExpr<InOp,OutOp> (. L := OutOp; .)
  {"&" Unary<Op> EqlExpr<Op,R>
    (. OutOp := MakeOp(zcBinaryAnd,[L,R]);
       L := OutOp; .)
  }
.

/*------------------------------------------------------------------------*/
EqlExpr<InOp : TZcOp; var OutOp : TZcOp>         (. var Op,L,R : TZcOp; Kind : TZcOpKind; .)
= RelExpr<InOp,OutOp> (. L := OutOp; Kind := zcNop; .)
  {( "!="   (. Kind := zcCompNE; .)
   | "=="   (. Kind := zcCompEQ; .)
   )
   Unary<Op> RelExpr<Op,R>
         (. OutOp := MakeBinary(Kind, L,R);
            L := OutOp;
         .)
  }
.
/*------------------------------------------------------------------------*/
RelExpr<InOp : TZcOp; var OutOp : TZcOp>         (. var Op,L,R : TZcOp;  Kind : TZcOpKind; .)
= ShiftExpr<InOp,OutOp> (. L := OutOp; Kind := zcNop; .)
  { ( "<"       (. Kind := zcCompLT; .)
    | ">"       (. Kind := zcCompGT; .)
    | "<="      (. Kind := zcCompLE; .)
    | ">="      (. Kind := zcCompGE; .)
     )
    Unary<Op> ShiftExpr<Op,R>
         (. OutOp := MakeBinary(Kind, L,R );
            L := OutOp;
         .)
  }
.
/*------------------------------------------------------------------------*/
ShiftExpr<InOp : TZcOp; var OutOp : TZcOp>         (. var Op,L,R : TZcOp; Kind : TZcOpKind; .)
= AddExpr<InOp,OutOp> (. L := OutOp; Kind := zcNop; .)
  { ("<<"  (. Kind := zcBinaryShiftL; .)
    | ">>" (. Kind := zcBinaryShiftR; .)
    )
    Unary<Op> AddExpr<Op,R>
    (. OutOp := MakeOp(Kind,[L,R]);
       L := OutOp; .)
  }
.
/*------------------------------------------------------------------------*/
AddExpr<InOp : TZcOp; var OutOp : TZcOp>         (. var Op,L,R : TZcOp;  Kind : TZcOpKind;.)
= MulExpr<InOp,OutOp>   (. L := OutOp; Kind := zcNop; .)
    {( "+"    (. Kind := zcPlus; .)
     | "-"    (. Kind := zcMinus; .)
     ) Unary<Op> MulExpr<Op,R>
         (. 
            if (L=nil) or (R=nil) then
              ZError('Invalid syntax');
            OutOp := MakeBinary(Kind,L,R);
            L := OutOp;
         .)
    }
.
/*------------------------------------------------------------------------*/
MulExpr<InOp : TZcOp; var OutOp : TZcOp>         (. var Tmp : TZcOp; Kind : TZcOpKind; .)
= (. OutOp := InOp; Kind := zcNop; .)
  { ("*"      (. Kind := zcMul; .)
    | "/"     (. Kind := zcDiv; .)
    | "%"     (. Kind := zcMod; .)
    ) Unary<Tmp>
         (. OutOp := MakeBinary(Kind,InOp,Tmp);
            InOp := OutOp;
         .)
    }
.
/*------------------------------------------------------------------------*/
Unary<var OutOp : TZcOp>         (. var LastOp,Tmp : TZcOp; Kind : TZcOpKind; .)
=       (. LastOp := nil; Kind := zcNop; .)
  {
    (
      /* "+" | */
      "-"   (. Kind := zcNegate; .) |
      "++"  (. Kind := zcPreInc; .) |
      "--"  (. Kind := zcPreDec; .) |
      "!"   (. Kind := zcNot; .)    |
      "~"   (. Kind := zcBinaryNot; .)
      /* | "~" | "*" */
    )
    (.
       if Kind in [zcNot,zcBinaryNot] then
       begin
         Tmp := MakeOp(Kind);
         if LastOp<>nil then
           LastOp.Children.Add(Tmp);
         LastOp := Tmp;
       end else if Assigned(LastOp) then
         ZError('- ,-- and ++ cannot be combined with other unary ops');
    .)
  }
  Primary<Tmp>
    (.
       if Kind=zcNegate then
         OutOp := MakeBinary(zcMinus, TZcOpLiteral.Create(Tmp.GetDataType.Kind,0), Tmp)
       else if Kind in [zcNop,zcNot,zcBinaryNot] then
       begin
         if LastOp<>nil then
         begin
           LastOp.Children.Add(Tmp);
           OutOp := LastOp;
         end else
           OutOp := Tmp;
       end else
       begin
         OutOp := MakePrePostIncDec(Kind,Tmp);
       end;
    .)
.



/*------------------------------------------------------------------------*/
InvokeArgument<var OutOp : TZcOp; Ci : TZComponentInfo>
(.
  var
    Op : TZcOp;
    Prop : TZProperty;
.)
= (. OutOp := nil; Op := nil; .)
  ident (.
    Prop := Ci.GetProperties.GetByName(LexString);
    if Prop=nil then
      ZError(LexString + ' is not a property of ' + Ci.ZClassName);
    if not (Prop.PropertyType in [zptFloat,zptInteger,zptByte,zptBoolean,zptString,zptComponentRef]) then
      ZError(LexString + ' property of ' + Ci.ZClassName + ' is of a datatype that cannot be set using inline component invokation syntax');
    OutOp := MakeOp(zcIdentifier);
    OutOp.Id := LexString;
  .)
  ":" Expr<Op>
  (. if Op=nil then
       ZError('Missing argument');
     Op := MakeCompatible(Op, PropTypeToZType(Prop.PropertyType));
     OutOp.Children.Add(Op);
  .)
.


/*------------------------------------------------------------------------*/
ReinterpretCast<var OutOp : TZcOp>
(.
var
  Op : TZcOp;
  Typ : TZcDataType;
.)
= "reinterpret_cast" "<" SimpleType<Typ> ">"
  "(" Expr<Op> ")"
  (.
     OutOp := TZcOpReinterpretCast.Create(nil);
     OutOp.Children.Add(Op);
     (OutOp as TZcOpReinterpretCast).Typ := Typ;
  .)
.

/*------------------------------------------------------------------------*/
InlineComponent<var OutOp : TZcOp>
(. var Op : TZcOp;
     Ci : TZComponentInfo;
.)
=  (. OutOp := nil;
      IsInInvokeArg := True;
   .)
   "@" ident (.
       Ci := ComponentManager.GetInfoFromName(LexString);
       OutOp := TZcOpInvokeComponent.Create(nil);
       OutOp.Id := LexString;
    .)
    "("
        [InvokeArgument<Op,Ci>       (. OutOp.Children.Add(Op); .)
          {"," InvokeArgument<Op,Ci> (. OutOp.Children.Add(Op); .)
          }
        ]
    ")" (. IsInInvokeArg := False; .)
.

/*------------------------------------------------------------------------*/
Primary<var OutOp : TZcOp>
(. var Op : TZcOp; S : string; V : double;
     Typ : TZcDataType;
.)
=            (. OutOp := nil; .)
  (
    ident    (. OutOp := CheckPrimary( MakeIdentifier(LexString) );  .)
  | InlineComponent<OutOp>
  | Literal<Typ>  (.
        if Typ.Kind in [zctString,zctNull] then
          OutOp := TZcOpLiteral.Create(Typ.Kind,LexString)
        else
        begin
          try
            S := LexString;
            V := ZcStrToFloat(S);
            OutOp := TZcOpLiteral.Create(Typ.Kind,V)
          except on EConvertError do
            SynError(200);
          end;
        end;
     .)
  | "(" Expr<OutOp> ")"
/*  | ( "bool"   | "byte"   | "char" | "decimal" | "double"
    | "float"  | "int"    | "long" | "object"  | "sbyte"
    | "short"  | "string" | "uint" | "ulong"   | "ushort"
    ) "." ident */
  | ReinterpretCast<OutOp>
  )

  {
  "++" (. OutOp := MakePrePostIncDec(zcPostInc,OutOp); .)
  | "--" (. OutOp := MakePrePostIncDec(zcPostDec,OutOp); .)

/*  | "->" ident */

  | "." (. IsInIdent := True; .) ident (.

      if not (OutOp.Kind in [zcIdentifier,zcSelect,zcFuncCall,zcArrayAccess]) then
        ZError('Invalid use of "."');

      Op := MakeOp(zcSelect,LexString);
      Op.Children.Add(OutOp);

      OutOp := CheckPrimary(Op);

      IsInIdent := False;

    .)

  | "(" (.
           if (OutOp=nil) or (OutOp.Kind<>zcIdentifier) then
             ZError('Unexpected "("');
           OutOp.Kind:=zcFuncCall;
         .)
        [Argument<Op> (. OutOp.Children.Add(Op); .)
          {"," Argument<Op> (. OutOp.Children.Add(Op); .)
          }
        ]
        (.
           if not VerifyFunctionCall(OutOp,S,CurrentFunction) then
             ZError(S);
        .)
    ")"

  | "[" (.
           if (OutOp=nil) or (not (OutOp.Kind in [zcIdentifier,zcSelect,zcArrayAccess,zcFuncCall])) then
             ZError('Unexpected "["');
           Op := MakeArrayAccess(OutOp);
           OutOp := Op;
         .)

        Expr<Op> (. OutOp.Children.Add( MakeCompatible(Op,zctInt) ); .)
          {"," Expr<Op> (. OutOp.Children.Add( MakeCompatible(Op,zctInt) ); .) }
        "]"
  }
.
/*------------------------------------------------------------------------*/
Literal<var Typ : TZcDataType>
= intCon (. Typ.Kind :=zctInt; .) |
  realCon (. Typ.Kind := zctFloat; .) |
  stringCon (. Typ.Kind := zctString; .)
  | "null" (. Typ.Kind := zctNull; .)
  /* | "true" | "false"  */
.

END Zc.
